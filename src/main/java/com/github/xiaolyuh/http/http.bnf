{
  parserClass="com.github.xiaolyuh.http.parser.HttpParser"
  parserUtilClass="com.github.xiaolyuh.http.parser.HttpParserUtil"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Http"
  psiImplClassSuffix="Impl"
  psiPackage="com.github.xiaolyuh.http.psi"
  psiImplPackage="com.github.xiaolyuh.http.psi.impl"

  elementTypeHolderClass="com.github.xiaolyuh.http.psi.HttpTypes"
  elementTypeClass="com.github.xiaolyuh.http.psi.HttpElementType"
  tokenTypeClass="com.github.xiaolyuh.http.psi.HttpTokenType"
  psiImplUtilClass="com.github.xiaolyuh.http.psi.impl.HttpPsiImplUtil"

  tokens=[
    T_LT='<'
    T_RT='>'
    PATH='regexp:([a-zA-Z0-9_\-./]+/?)+'
    URL_DESC='regexp:https?://[-a-zA-Z0-9+&@${}()#/%?=~_|!:,.;]*[-a-zA-Z0-9+,&@${}()#/%=~_|]'
    HEADER_DESC='regexp:[a-zA-Z\-]+:[a-zA-Z0-9,${}()=;\\".\-/ ]+'
    REQUEST_COMMENT='regexp:#.*'
    LINE_COMMENT='regexp://.*'
    URL_FORM_ENCODE="regexp:[a-zA-Z0-9,&$={}()]*"
    JSON_TEXT="regexp:[{\[][a-zA-Z0-9._,\":'&${}()\[\]\s]*"
    XML_TEXT="regexp:<[a-zA-Z0-9.()$/,<>]*"
    MULTIPART_SEPERATE='regexp:--[^-]+'
    MULTIPART_SEPERATE_END='regexp:--[^-]+--'
    VARIABLE='regexp:\{\{[a-zA-Z0-9.(),$]+}}'
    JS_SCRIPT='regexp:\{%[^~]*%}'
    SPACE='regexp:\s+'
  ]
}
httpFile ::= request*
request ::= method url version? headers? body? script? {
    pin = 1
}
url ::= URL_DESC
version ::= 'HTTP/1.1' | 'HTTP/2.0'
method ::= GET | POST | DELETE | PUT
headers ::= header*
header ::= HEADER_DESC
body ::= ordinary_content | multipart_content
script ::= T_RT JS_SCRIPT
ordinary_content ::= file | URL_FORM_ENCODE | JSON_TEXT |XML_TEXT {
    mixin = "com.github.xiaolyuh.http.inject.HttpPsiLanguageInjectionHost"
}
file ::= T_LT file_path
file_path ::= PATH
multipart_content ::= multipart_body* MULTIPART_SEPERATE_END
multipart_body ::= MULTIPART_SEPERATE header* ordinary_content